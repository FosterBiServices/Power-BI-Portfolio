Sub ChangeUserFormAndControlsSize()
    Dim MyUserform As Object
    Dim ControlOnForm As Object
    Dim NameUserform As String

    'You can change the 1.3, use lower then 1 to make it smaller
    Const SizeCoefficient = 1.5

    'Enter the name of the UserForm
    NameUserform = "Userform3"

    Set MyUserform = ThisWorkbook.VBProject.VBComponents(NameUserform)
    With MyUserform
        .Properties("Height") = .Properties("Height") * SizeCoefficient
        .Properties("Width") = .Properties("Width") * SizeCoefficient
    End With

    For Each ControlOnForm In MyUserform.Designer.Controls
        With ControlOnForm
            .Top = .Top * SizeCoefficient
            .Left = .Left * SizeCoefficient
            .Width = .Width * SizeCoefficient
            .Height = .Height * SizeCoefficient
            On Error Resume Next
            .Font.Size = .Font.Size * SizeCoefficient
            On Error GoTo 0
        End With
    Next ControlOnForm

End Sub